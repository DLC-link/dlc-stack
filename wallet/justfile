set dotenv-load := true

##############################
# utils

clippy:
  cargo clippy -- -Aclippy::drop_non_drop

clippy-mac:
  cd ./node-manager && AR=/opt/homebrew/opt/llvm/bin/llvm-ar CC=/opt/homebrew/opt/llvm/bin/clang cargo clippy -- -Aclippy::drop_non_drop


##############################
# wallet-blockchain-interface

build-wbi:
  cd ./wallet-blockchain-interface && npm i && npm run build

wbi: build-wbi
  cd ./wallet-blockchain-interface && npm run start


##############################
# wallet

build-wallet:
  cargo build --release --bins --target-dir .

generate-key: build-wallet
  ./release/generate-key

wallet: build-wallet
  ./release/dlc-protocol-wallet

generate-address-from-key: build-wallet
  ./release/generate-address-from-key


##############################
# both

build: build-wbi build-wallet

run: build
  TEST_MODE_ENABLED=$TEST_MODE_ENABLED just wbi &
  XPRIV=${XPRIV} FINGERPRINT=${FINGERPRINT} just wallet && fg
